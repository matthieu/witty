descriptions = L()

macro(describe(`str, `descBody), `(
  lambda(
    cases = L()
    before = null
 
    macro(before(`freq, `befBody), `(
      if($freq == "each", before = lambda($befBody))
    ))
    macro(it(`desc, `itBody), `(
      push(cases, L($desc, lambda($itBody)))
    ))
    $descBody

    push(descriptions, L("describe", $str, before, cases));
  )()
))

load("test/arithmetics.wy")
load("test/list.wy")
load("test/hash.wy")
load("test/control.wy")
load("test/lambda.wy")
load("test/macro.wy")

failures = L()
testCount = 0
for(descriptions, lambda(desc,
  print(desc@1)
  for(desc@3, lambda(case,
    caseBody = case@1
    if (desc@2,
      bef = desc@2
      bef()
    )
    success = caseBody()
    print("  " + case@0 + " -> " + if(success, "OK", "FAILED"))
    testCount += 1
    if(!success, push(failures, desc@1 + " " + case@0))
  ))
))

print("")
print(testCount + " tests were executed.")
if(length(failures) > 0,
  print("There were " + length(failures) + " failure(s)!")
  for(failures, lambda(f, print("  " + f)))
)

// generalized variables
// provide primitives allowing read/write access to witty code so code can be created or altered directly
// basic list operations (push, pop, ...)
// empty function for strings and lists
// real exceptions with script line error numbers
// regexps
// pretty printer for all data types
// assign with := returns old value to allow a = b := a (exchange value)
// single param lambdas syntax sugar: \(..)
// lambdas shortcut y(...) ?
// multiline strings
// recursive lambdas when they're not named
// macro including macros
// recursive macros
// function composition
// delay and force
